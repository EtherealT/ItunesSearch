plugins {
    id 'java'
    id 'idea'
    id 'maven'
    id 'jacoco'
    id 'eclipse'
    id "net.saliman.cobertura" version "2.6.0"
    id 'com.github.kt3k.coveralls' version '2.6.3'
}

group 'me.tobiadeyinka'
version '1.5.1'

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8

jar {
    baseName = 'iTunesSearch'
    version =  project.version
}

dependencies {
    compile group: 'org.json', name: 'json', version: '20180130'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    compile group: 'com.neovisionaries', name: 'nv-i18n', version: '1.22'

    testCompile group: 'org.testng', name: 'testng', version: '6.14.2'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.9.1'
}

test {
    useTestNG() {
        suites "src/test/resources/test_suites/all_tests.xml"
    }

    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
    }
}

cobertura.coverageFormats = ['html', 'xml'] // coveralls plugin depends on xml format report

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

coveralls {
    jacocoReportPath = "${buildDir}/reports/coverage/debug/report.xml"
}

tasks.coveralls {
    dependsOn 'test'
    onlyIf { System.env.'CI' }
}
